;
;R01 01.08.2001 FIX BUG INT "SELECT_IDE"
;R00 24.07.2001 ADD SECONDARY IDE
;INITIAL NEW VERSION (2.48) 24.07.2001
;========================================================
;Write
HDW_3F7	EQU	#4155	;3F7H Command
HDW_3F6	EQU	#4154	;3F6H Device Control

HDW_COM	EQU	#4153	;1F7H Command
HDW_DRV	EQU	#4152	;1F6H Drive Control

HDW_CLH	EQU	#0155	;1F5H Cylinder High
HDW_CLL	EQU	#0154	;1F4H Cylinder Low
HDW_SEC	EQU	#0153	;1F3H Sector
HDW_CNT	EQU	#0152	;1F2H Counter
HDW_ERR	EQU	#0151	;1F1H Error
HDW_DAT	EQU	#0150	;1F0H Data
;Read
HDR_3F7	EQU	#4055	;3F7H Drive Address
HDR_3F6	EQU	#4054	;3F6H Alt. Status

HDR_CTL	EQU	#4053	;1F7H Status (Control)
HDR_DRV	EQU	#4052	;1F6H Drive Control

HDR_CLH	EQU	#0055	;1F5H Cylinder High
HDR_CLL	EQU	#0054	;1F4H Cylinder Low
HDR_SEC	EQU	#0053	;1F3H Sector
HDR_CNT	EQU	#0052	;1F2H Counter
HDR_ERR	EQU	#0051	;1F1H Error
HDR_DAT	EQU	#0050	;1F0H Data

BSY	EQU	7
RDY	EQU	6
DRQ	EQU	3
ERR	EQU	0

HDD	EQU	#01
CDROM	EQU	#02

IDE__CD	CALL	SELECT_IDE
;MASTER
CDAUTO	LD	A,CDROM
	LD	(IDEDEV),A
	LD	A,#FF
	JP	CDMASTR

SELECT_IDE
	AND	A	;%00000011
	LD	D,#A0
	JR	Z,IAUTO0
	DEC	A
	LD	D,#B0
	JR	Z,IAUTO0

	IF	WITH_2IDE=1

	DEC	A
	LD	D,#A0	;R00
	JR	Z,IAUTO1;R00
	DEC	A	;R00
	LD	D,#B0	;R00
IAUTO1	LD	A,#01	;R00 ;SELECT SECONDARY
	OUT	(#BC),A	;R00
	LD	A,1
	JR	IAUTO	;R00

	ENDIF

IAUTO0	LD	A,#21	;R00 ;SELECT PRIMARY
	OUT	(#BC),A	;R00
	LD	A,0
IAUTO	LD	(ICHANEL),A
	LD	BC,HDW_DRV	;R01
	OUT	(C),D		;R01
	RET

IDEAUTO	CALL	SELECT_IDE
;MASTER
	LD	A,#FF
	LD	(IDEDEV),A
CDMASTR	LD	(SKIP),A
	LD	BC,HDW_DRV
	OUT	(C),D

	LD	BC,HDR_CTL
	IN	A,(C)
	AND	#80
	LD	HL,280
	JR	Z,NO_BUSY
	LD	HL,1550
	EI 
CLRBUSY	HALT 
	DEC	HL
	LD	A,H
	OR	L
	JP	Z,ABSENT
	CALL	SKIPKEY
	JP	C,ABSENT
	LD	BC,HDR_CTL
	IN	A,(C)
	AND	#80
	JR	NZ,CLRBUSY

NO_BUSY	LD	E,#05
	LD	BC,HDW_CNT
	OUT	(C),E
	LD	BC,#0010
	DJNZ	$
	DEC	C
	JR	NZ,$-3
	LD	BC,HDR_CNT
	IN	A,(C)
	CP	E
	JP	NZ,ABSENT

	LD	A,(IDEDEV)
	CP	CDROM
	JP	Z,NOHDD

	LD	E,#00		;NOP
	LD	BC,HDW_COM
	OUT	(C),E

WXREADY	HALT 
	DEC	HL
	LD	A,H
	OR	L
	JP	Z,ABSENT
	CALL	SKIPKEY
	JP	C,ABSENT
	LD	BC,HDR_CTL
	IN	A,(C)
	AND	#C0
	CP	#40
	JR	NZ,WXREADY

;	LD	A,#90		;
;	CALL	IDE_CMD

	LD	A,HDD
	LD	(IDEDEV),A
	LD	E,#EC		;IDENTIFY ATA
	LD	BC,HDW_COM
	OUT	(C),E
	LD	B,#00
	DJNZ	$
	LD	HL,(WAITSML)
	LD	DE,#0101
	LD	BC,HDR_CTL
	CALL	WAITPRT
	JP	NC,NOHDD

GETPARM	LD	HL,(WAITIDE)
	LD	DE,#0808
	LD	BC,HDR_CTL
	CALL	WAITPRT
	JP	C,ABSENT
	LD	BC,HDR_DAT
	LD	HL,TEMP
	INIR 
	INIR 
	CALL	IDESPEC
	AND	A
	RET 

NOHDD	LD	A,CDROM
	LD	(IDEDEV),A
	LD	E,#A1		;IDENTIFY ATAPI
	LD	BC,HDW_COM
	OUT	(C),E
	LD	B,#00
	DJNZ	$
	LD	HL,(WAITSML)
	LD	DE,#0101
	LD	BC,HDR_CTL
	CALL	WAITPRT
	JP	C,GETPARM
ABSENT	SCF 
	RET 

IDESPEC	IN	A,(PAGE3)
	EX	AF,AF'
	LD	A,SYSPAGE
	OUT	(PAGE3),A
	LD	A,(IDEDEV)
	LD	(IY+DTYPE_H),A
	CP	CDROM
	JP	Z,FOR_CDR
	LD	BC,HDR_DRV
	IN	A,(C)
	AND	#F0
	LD	B,A
	LD	A,(TEMP+#06)	;HEADS PER TRACK
	LD	(IY+HEADS_H),A
	DEC	A
	AND	#0F
	OR	B
	LD	B,A
	LD	A,(TEMP+#63)	;LBA/NON-LBA bit 1 (FROM ZERO!)
	BIT	1,A
	JR	Z,NONLBA
	SET	6,B
NONLBA	LD	A,B
	LD	BC,HDW_DRV
	OUT	(C),A
	AND	#F0
	LD	HL,ICHANEL
	OR	(HL)
	LD	(IY+DRVHD_H),A
	LD	HL,(TEMP+#02)	;CYLINDERS
	LD	(IY+CYL_L_H),L
	LD	(IY+CYL_H_H),H
	LD	A,(TEMP+#0C)	;SECTOR	PER TRACK
	LD	(IY+SC_PT_H),A
	LD	BC,HDW_CNT
	OUT	(C),A
	LD	A,#91		;
	CALL	IDE_CMD
;	RET	C
	LD	C,(IY+SC_PT_H)	  ; Sector per track
	LD	B,0
	LD	A,(IY+HEADS_H)	  ; Head per HDD
	LD	HL,0
HDDINI3	ADD	HL,BC
	DEC	A
	JR	NZ,HDDINI3
	LD	(IY+SPCLL_H),L
	LD	(IY+SPCLH_H),H
NOSPEC	EX	AF,AF'
	OUT	(PAGE3),A
	AND	A
	RET 

FOR_CDR	LD	BC,HDR_DRV
	IN	A,(C)
	AND	#F0
	LD	HL,ICHANEL
	OR	(HL)
	LD	(IY+DRVHD_H),A
	JR	NOSPEC

IDE_CMD	PUSH	AF
	LD	HL,(WAITIDE)
	LD	DE,#C040
	LD	BC,HDR_CTL
	CALL	WAITPRT
	POP	DE
	RET	C
	LD	BC,HDW_COM
	OUT	(C),D
	LD	HL,(WAITIDE)
	LD	DE,#C040
	LD	BC,HDR_CTL
	JP	WAITPRT

WAITHDD	EI 
	LD	HL,1533
WTREADY	HALT 
	LD	BC,HDR_CTL
	IN	A,(C)
	AND	#C0
	CP	#40
	RET	Z
	DEC	HL
	LD	A,H
	OR	L
	JR	NZ,WTREADY
	SCF 
	RET 

;WAITHDD DEC	 L
;	 RET	 NZ
;	 DEC	 H
;	 RET	 NZ
;	 DEC	 E
;	 RET	 NZ
;	 SCF
;	 RET

; E - Second * 10

;PAUSE	 LD	 HL,#0000
;PAUSE1	 DEC	 L
;	 JR	 NZ,PAUSE1
;	 DEC	 H
;	 JR	 NZ,PAUSE1
;	 DEC	 E
;	 JR	 NZ,PAUSE1
;	 RET

; D - MASK
; E - PATTERN
; BC - PORT

WAITPRT	IN	A,(C)
;	CP	#FF
;	JR	Z,WAITP1
	AND	D
	CP	E
	JR	NZ,WAITP2
	AND	A
	RET 

WAITP2	DEC	HL
	CALL	SKIPKEY
	RET	C
	LD	A,L
	OR	H
	JP	NZ,WAITPRT
WAITP1	SCF 
	RET 

SKIPKEY	EXX 
	CALL	SCANKEY
	EXX 
	SCF 
	CCF 
	RET	Z
	EXX 
	LD	HL,#3E00
	AND	A
	SBC	HL,DE
	EXX 
	SCF 
	CCF 
	RET	NZ
	LD	A,0
	LD	(SKIP),A
	SCF 
	RET 

WAITIDE	DW	#0000

WAITSML	DW	#0400

SKIP	DB	#FF

IDEDEV	DB	#FF

ICHANEL	DB	#00

DRVHD_H	EQU	0
SC_PT_H	EQU	1
HEADS_H	EQU	2
CYL_L_H	EQU	3
CYL_H_H	EQU	4
SPCLL_H	EQU	5
SPCLH_H	EQU	6
DTYPE_H	EQU	7

IDE0	EQU	#C1C0
IDE1	EQU	#C1C8
IDE2	EQU	#C1D0
IDE3	EQU	#C1D8

;IDE0	 DB	 #FF	 ;DRIVE/HEAD REGISTER	      ;00
;	 DB	 #FF	 ;SECTORS PER TRACK	      ;01
;	 DB	 #FF	 ;HEADS			      ;02
;	 DB	 #FF	 ;CYLINDERS LOW		      ;03
;	 DB	 #FF	 ;CYLINDERS HIGH	      ;04
;	 DB	 #FF	 ;SECTOR PER CYLINDER LOW     ;05
;	 DB	 #FF	 ;SECTOR PER CYLINDER HIGH    ;06
;	 DB	 #FF	 ;DEVICE TYPE		      ;07

;IDE1	 DB	 #FF	 ;DRIVE/HEAD REGISTER	      ;00
;	 DB	 #FF	 ;SECTORS PER TRACK	      ;01
;	 DB	 #FF	 ;HEADS			      ;02
;	 DB	 #FF	 ;CYLINDERS LOW		      ;03
;	 DB	 #FF	 ;CYLINDERS HIGH	      ;04
;	 DB	 #FF	 ;SECTOR PER CYLINDER LOW     ;05
;	 DB	 #FF	 ;SECTOR PER CYLINDER HIGH    ;06
;	 DB	 #FF	 ;DEVICE TYPE		      ;07
