ORG_	ORG	#8000
IPOINT	EQU	8
PAGE0	EQU	#82
PAGE1	EQU	#A2
PAGE2	EQU	#C2
PAGE3	EQU	#E2
	DI 
	XOR	A
	OUT	(#82),A
	LD	A,1
	OUT	(#FD),A
	CALL	FOR
	XOR	A
	OUT	(#FD),A
	EI 
	RET 

FOR	LD	A,2
	LD	C,0
	RST	#18
	LD	(HDDS),A
	LD	A,2
	LD	C,1
	RST	#10
	LD	BC,#0380
	LD	E,1
	CALL	IPOINT
	LD	DE,0
	LD	BC,#4789
	LD	HL,#2050
	CALL	IPOINT

	LD	BC,#08C2
	CALL	IPOINT
	JP	C,EXIT
	LD	(MEM_ID),A
	LD	BC,#00C4
	LD	HL,FATPAGE
GMEM1	PUSH	BC
	PUSH	HL
	LD	A,(MEM_ID)
	CALL	IPOINT
	POP	HL
	POP	BC
	LD	(HL),A
	INC	HL
	INC	B
	LD	A,8
	CP	B
	JR	NZ,GMEM1
	LD	DE,#0000
	CALL	LOCAT
	CALL	CRLF
	CALL	CHOICE
	JP	C,EXIT
	LD	(FDRIVE),A
	CALL	CRLF
	CALL	PROMPT
	JP	C,EXIT
	CALL	CRLF
	CALL	CRLF
	CALL	SIZEDRV
	PUSH	HL
	LD	HL,FORMES
	CALL	PRINTZ
	POP	HL
	CALL	PDIGIT
	LD	A,"M"
	CALL	PRINT
	CALL	CRLF
	LD	HL,0
	LD	(PERS),HL
	LD	(BAD_CLU),HL
	CALL	INSTALS
	CALL	CLR_FAT
	CALL	PERSENT
	LD	HL,0
	LD	DE,(CLUSTER)
	LD	BC,100
	CALL	DIV32
	LD	(PFACTOR),IX
	LD	HL,(PFACTOR)
	LD	DE,(CLUSTER)
	LD	A,R
	ADD	A,E
	LD	E,D
	LD	D,A
	ADD	HL,DE
	LD	A,L
	XOR	H
	LD	L,A
	LD	(RND+1),HL
	LD	HL,2
	LD	(CURCLUS),HL
AGAIN	CALL	VSECTOR
	LD	HL,(CURCLUS)
	CALL	CSECTOR
	LD	HL,(CURCLUS)
	INC	HL
	LD	(CURCLUS),HL
	EX	DE,HL
	LD	HL,(MAXCLUS)
	AND	A
	SBC	HL,DE
	JP	C,COMPLET

	LD	HL,0
	LD	DE,(CURCLUS)
	LD	BC,(PFACTOR)
	CALL	DIV32
	LD	B,HX
	LD	C,LX
	LD	HL,(PERS)
	AND	A
	SBC	HL,BC
	JP	Z,AGAIN
	LD	(PERS),BC
	CALL	PERSENT
	JP	AGAIN

COMPLET	LD	HL,0
	LD	DE,(CURCLUS)
	LD	BC,(PFACTOR)
	CALL	DIV32
	LD	B,HX
	LD	C,LX
	LD	(PERS),BC
	CALL	PERSENT
	CALL	PREORD
	CALL	COMP
	CALL	SAVEFAT
	CALL	ORDER
	CALL	CRLF
	LD	HL,ANOTHER
	CALL	PRINT_D
	CALL	KEY
	JP	EXIT

INSTALS
	LD	HL,(SIZE_H)
	LD	DE,(SIZE_L)
	LD	(ALLSECL),DE
	LD	(ALLSECH),HL
	LD	BC,#FFF0
	CALL	DIV32
	LD	A,H
	OR	L
	LD	C,LX
	JR	Z,EQU0
	INC	C
EQU0	LD	A,C
	CP	5
	LD	A,1
	JR	NC,EQU1
	LD	A,4
	JR	SKIPSPC
EQU1	SRL	C
	RLCA 
	JR	NZ,EQU1
SKIPSPC	LD	(S_P_C),A
	LD	HL,(SIZE_H)
	LD	DE,(SIZE_L)
	LD	B,#00
	LD	C,A
	CALL	DIV32
	LD	A,H
	OR	L
	JR	Z,EQU2
	INC	IX
EQU2	LD	A,LX
	OR	A
	LD	A,HX
	JR	Z,EQU3
	INC	A
EQU3	LD	(S_P_FAT),A

	LD	HL,(SECTORS)
	LD	(S_P_T),HL
	LD	(HIDD_L),HL
	LD	HL,0
	LD	(HIDD_H),HL
	LD	HL,(HEADS)
	LD	A,(CTRLREG)
	BIT	6,A
	CALL	NZ,LBAXCHG
	LD	(H_P_C),HL
	PUSH	IY
	LD	IY,BPB_
	LD	HL,0	      ;	calc. first sector FAT
	LD	E,(IY+#0E)    ;Reserve sec
	LD	D,(IY+#0F)
	ADD	HL,DE
	LD	E,(IY+#1C)    ;Hidden sec
	LD	D,(IY+#1D)
	ADD	HL,DE
	LD	(FAT1_XX),HL  ;	first sector FAT #1
	LD	(FAT2_XX),HL

	LD	E,(IY+#16)    ;	sectors	in FAT
	LD	D,(IY+#17)
	LD	A,(FAT_P_D)   ;	amount FATs
	CP	1
	JR	Z,C_DATA1
	DEC	A
	ADD	HL,DE
	LD	(FAT2_XX),HL
C_DATA1	ADD	HL,DE
	DEC	A
	JR	NZ,C_DATA1
	LD	(DIR_FRM),HL  ;	first sector DIR

	LD	C,(IY+#0B)    ;	Size sectors
	LD	B,(IY+#0C)
	RL	C
	RL	B
	RL	C
	RL	B
	RL	C
	RL	B
	LD	C,B
	LD	B,0	      ;	BC - File handels in sectors
	LD	E,(IY+#11)    ;	Number file handel
	LD	D,(IY+#12)
	EX	DE,HL
	DEC	HL
	XOR	A
NEXTAD2
	INC	A
	JP	Z,ERR_BPB
	SBC	HL,BC
	JR	NC,NEXTAD2
	EX	DE,HL
	LD	C,A	      ;	A - sectors in DIR
	LD	B,0
	LD	(DIR_S_S),A
	ADD	HL,BC	      ;	Start DATA area
	LD	(DAT_FRM),HL

	LD	HL,(DAT_FRM)
	LD	E,(IY+#1C)    ;Hidden sec
	LD	D,(IY+#1D)
	AND	A
	SBC	HL,DE
	EX	DE,HL
	LD	L,(IY+#13)
	LD	H,(IY+#14)
	LD	A,H
	OR	L
	JP	NZ,HDDSMAL
	LD	L,(IY+#20)
	LD	H,(IY+#21)
	LD	C,(IY+#22)
	LD	B,(IY+#23)
	AND	A
	SBC	HL,DE
	JP	NC,HDDBIG
	DEC	BC
	JP	HDDBIG
HDDSMAL	AND	A
	SBC	HL,DE
	LD	BC,0
HDDBIG	LD	A,(S_P_C)
	SCF 
S4C01	RRA 
	JR	C,S4C02
	RR	B
	RR	C
	RR	H
	RR	L
	JP	S4C01
S4C02	;NC	HL
	LD	(CLUSTER),HL
	INC	HL
	LD	(MAXCLUS),HL
	POP	IY
	XOR	A
	RET 

ERR_BPB	POP	IY
	SCF 
	RET 

; HL:DE	* BC =>	HL:IX
LBAXCHG	EX	DE,HL
	LD	HL,0
	LD	BC,(CYLRS)
	CALL	MUL32
	LD	D,HX
	LD	E,LX
	EX	DE,HL
	LD	BC,1024
	CALL	DIV32
	LD	A,LX
	LD	DE,#0080
LBAX1	SLA	A
	JR	C,LBAX2
	SRL	D
	RR	E
	JR	LBAX1
LBAX2	OR	A
	JR	Z,LBAX3
	SLA	E
	RL	D
LBAX3	XOR	A
	CP	D
	JR	Z,LBAX4
	LD	DE,#00FF
LBAX4	EX	DE,HL
	RET 

FAT1_XX	DEFW	#0000	; first	sector FAT #1
FAT2_XX	DEFW	#0000	; first	sector FAT #2
DIR_FRM	DEFW	#0000	; first	sector DIR
DIR_S_S	DEFB	#00	; size root in sectors
DAT_FRM	DEFW	#0000	; first	sector DATA

PLUS	DEFW	#0000

VSECTOR	LD	DE,(CUR_POS)
	LD	E,#20
	CALL	HLOCAT
	LD	HL,(CURCLUS)
	PUSH	HL
	LD	A,H
	CALL	PHEX
	POP	HL
	LD	A,L
	JP	PHEX

EXIT	LD	A,(MEM_ID)
	LD	C,#C3
	CALL	IPOINT
	LD	BC,#0080
	LD	E,0
	CALL	IPOINT
	LD	IY,#5C3A
	RET 

;    DE	- CLUSTER
; IX:DE	- SECTOR
CSECTOR	DEC	HL
	DEC	HL
	EX	DE,HL
	LD	A,(S_P_C)
	LD	B,A
	LD	HL,0
	LD	IX,0
ADD_DE1	ADD	HL,DE
	JP	NC,ADD_DE2
	INC	IX
ADD_DE2	DJNZ	ADD_DE1
	LD	DE,(DAT_FRM)
	ADD	HL,DE
	JP	NC,ADD_DE3
	INC	IX
ADD_DE3	EX	DE,HL
;      IX:DE - SECTOR
	LD	BC,#0208
	LD	A,(S_P_C)
	LD	L,A
	LD	A,(FDRIVE)
	SUB	"A"
	RST	#18
	RET	NC
	LD	HL,(BAD_CLU)
	INC	HL
	LD	(BAD_CLU),HL
	LD	DE,(CUR_POS)
	LD	E,#30
	CALL	HLOCAT
	LD	HL,(CURCLUS)
	PUSH	HL
	LD	DE,#FFF7
	CALL	MARKFAT
	POP	HL
	CALL	PDIGIT
	RET 

PROMPT	CALL	CRLF
	LD	HL,FP1
	CALL	PRINT_D
	LD	HL,FP2
	CALL	PRINT_D
	LD	HL,FP4
	CALL	PRINTZ
WPR	CALL	KEY
	RES	5,A
	CP	"Y"
	RET	Z
	CP	"N"
	JP	NZ,WPR
	SCF 
	RET 


PERSENT	CALL	CR
	LD	HL,(PERS)
	CALL	PDIGIT
	LD	HL,PERSEN
	JP	PRINTZ

COMP	CALL	CR
	LD	HL,PERCOM
	JP	PRINTZ

CHOICE	CALL	CRLF
	LD	HL,HDFOR
	CALL	PRINT_D
	CALL	CRLF
	LD	HL,CHOLET
	CALL	PRINTZ
	LD	A,(HDDS)
	OR	A
	JP	Z,NOHDDS
	LD	B,A
	LD	C,0
CHOL1	LD	A,"C"
	ADD	A,C
	PUSH	BC
	CALL	PRINT
	LD	A," "
	CALL	PRINT
	LD	A," "
	CALL	PRINT
	POP	BC
	INC	C
	DJNZ	CHOL1
	LD	A,"C"
	ADD	A,C
	LD	(CHOP2+1),A
CHOP	CALL	KEY
	RES	5,A
	CP	"C"
	JR	C,CHOP
CHOP2	CP	"D"
	JP	NC,CHOP
	AND	A
	RET 

NOHDDS	LD	HL,NOHD
	CALL	PRINTZ
	CALL	KEY
	SCF 
	RET 

PREORD	LD	IX,(CLUSTER)
	CALL	IX_CLC
	LD	DE,ORDER1+12
	CALL	REMORF
	LD	IX,(BAD_CLU)
	CALL	IX_CLC
	LD	DE,ORDER2+12
	CALL	REMORF
	LD	HL,(CLUSTER)
	LD	DE,(BAD_CLU)
	AND	A
	SBC	HL,DE
	EX	DE,HL
	LD	HX,D
	LD	LX,E
	CALL	IX_CLC
	LD	DE,ORDER5+12
	CALL	REMORF
	LD	A,(S_P_C)
	LD	H,A
	LD	L,0
	SLA	L
	RL	H
	EXX 
	LD	HL,0
	EXX 
	CALL	MAKE_LN
	LD	DE,ORDER3+12
	CALL	REMORF
	LD	HL,(CLUSTER)
	LD	DE,(BAD_CLU)
	AND	A
	SBC	HL,DE
	EXX 
	LD	HL,0
	EXX 
	CALL	MAKE_LN
	LD	DE,ORDER4+12
	CALL	REMORF
	CALL	GSERIAL
	RET 

IX_CLC	LD	HL,0
	LD	A,(S_P_C)
PREORD1	RRCA 
	JR	C,PREORD2
	ADD	IX,IX
	ADC	HL,HL
	JR	PREORD1

PREORD2	LD	D,L
	LD	E,HX
	LD	A,LX
	LD	H,A
	LD	L,0
	SLA	L
	RL	H
	RL	E
	RL	D
	PUSH	DE
	EXX 
	POP	HL
	EXX 
	JP	MAKE_LN

ORDER	CALL	CRLF
	CALL	CRLF
	LD	HL,ORDER1
	CALL	PRINT_D
	LD	HL,ORDER2
	CALL	PRINT_D
	LD	HL,ORDER5
	CALL	PRINT_D
	CALL	CRLF
	LD	HL,ORDER3
	CALL	PRINT_D
	LD	HL,ORDER4
	CALL	PRINT_D
	CALL	CRLF
	LD	HL,ORDER6
	CALL	PRINTZ
	LD	A,(SERIAL+3)
	CALL	PHEX
	LD	A,(SERIAL+2)
	CALL	PHEX
	LD	A,"-"
	CALL	PRINT
	LD	A,(SERIAL+1)
	CALL	PHEX
	LD	A,(SERIAL+0)
	CALL	PHEX
	CALL	CRLF
	RET 

SIZE_L	DEFW	#0000
SIZE_H	DEFW	#0000
MAXCLUS	DEFW	#0000
CURCLUS	DEFW	#0000

SIZE_MB	DEFW	#0000
CYLRS	DEFW	#0000
HEADS	DEFW	#0000
SECTORS	DEFW	#0000
CTRLREG	DEFB	#00

PREVBAD	DEFW	#0000
BAD_CLU	DEFW	#0000
CLUSTER	DEFW	#0000

PFACTOR	DEFW	#0000
PERS	DEFW	#0000

HDDS	DEFB	#00

PERSEN	DEFB	" persent completed",0

PERCOM	DEFB	"Format	complete      ",0

FP1	DEFB	"WARNING, ALL DATA ON NON-REMOVABLE DISK",0
FP2	DEFB	"DRIVE "
FDRIVE	DEFB	"C: WILL BE LOST!",0
FP4	DEFB	"Proceed with Format (Y/N)?",0

ANOTHER	DEFB	"Format	another	(Y/N)?",0

FORMES	DEFB	"Formatting ",0

HDFOR	DEFB	"Hard disk formatter v1.00 (c) 1998 Peters+",0
CHOLET	DEFB	"Choose	drive letter: ",0

NOHD	DEFB	"Hard Disk(s) not found",0

ORDER1	DEFB	"	       total disk space",0
ORDER2	DEFB	"	       in bad sectors",0
ORDER5	DEFB	"	       free on disk",0
ORDER3	DEFB	"	       in each allocation unit",0
ORDER4	DEFB	"	       allocation unit available on disk",0
ORDER6	DEFB	"Serial	Number : ",0

KEY	EI 
	LD	C,#30
	RST	#10
	RET 

SIZEDRV	LD	A,(FDRIVE)
	SUB	"C"
	ADD	A,2
	LD	DE,#55AA
	LD	BC,#0008
	RST	#18
	RET	C
	LD	(SIZE_L),DE
	LD	(SIZE_H),HL
	LD	(CTRLREG),A
	EXX 
	LD	(CYLRS),HL
	LD	(HEADS),DE
	LD	(SECTORS),BC
	EXX 
	LD	A,D
	LD	D,H
	LD	H,L
	LD	L,A
;			  E
;	LD	L,(IX+5)  D
;	LD	H,(IX+6)  L
;	LD	D,(IX+7)  H
	XOR	A
	SRL	D
	RR	H
	RR	L
	RR	A
	SRL	D
	RR	H
	RR	L
	RR	A
	SRL	D
	RR	H
	RR	L
	RR	A
	OR	A
	LD	(SIZE_MB),HL
	RET	Z
	INC	HL
	LD	(SIZE_MB),HL
	RET 

CUR_POS	DEFW	#0000

PRINT	LD	BC,#0182
	JP	IPOINT

PRINTB	LD	C,#82
	JP	IPOINT

STRING	CALL	LOCAT
PRINTZ	LD	BC,#508C
	LD	D,0
	JP	IPOINT

PRINT_D	LD	BC,#508C
	LD	D,0
	CALL	IPOINT
	JP	CRLF

PRINTC	LD	C,#86
	JP	IPOINT

CR	LD	DE,(CUR_POS)
	LD	E,0
	JP	LOCAT

CRLF	LD	DE,(CUR_POS)
	LD	E,0
	LD	A,#1F
	CP	D
	JR	NZ,CRLF2
	PUSH	DE
	LD	DE,#0020
	LD	BC,#018A
	CALL	IPOINT
	LD	DE,#1F00
	CALL	HLOCAT
	LD	A,#20
	LD	B,80
	CALL	PRINTB
	POP	DE
	JR	LOCAT
CRLF2	INC	D
LOCAT	LD	C,#84
	LD	(CUR_POS),DE
	JP	IPOINT

HLOCAT	LD	C,#84
	JP	IPOINT

PDIGIT	LD	DE,10000
	LD	A,#C8
	LD	(RET_Z),A
	CALL	DIG
	LD	DE,1000
	CALL	DIG
	LD	DE,100
	CALL	DIG
	LD	DE,10
	CALL	DIG
	LD	A,L
	ADD	A,#30
	JP	PRINT

DIG	XOR	A
DIG1	INC	A
	SBC	HL,DE
	JR	NC,DIG1
	ADD	HL,DE
	DEC	A
RET_Z	RET	Z
	ADD	A,#30
	PUSH	HL
	CALL	PRINT
	POP	HL
	LD	A,0
	LD	(RET_Z),A
	RET 

;	HL*DE => HL
MUL16	PUSH	HL ;HL*DE--HL
	XOR	A
	OR	D
	JR	Z,MULENT
	EX	DE,HL
	XOR	A
	OR	D
	JR	Z,MULENT
	SCF 
	POP	HL
	RET 

MULENT	LD	A,E
	LD	E,0
	LD	D,E
	JR	MMULB2
MMULB1	ADD	HL,HL
	JR	C,ENDMUL
MMULB2	OR	A
	JR	Z,ENDMUL
	RRA 
	JR	NC,MMULB1
	EX	DE,HL
	ADD	HL,DE
	EX	DE,HL
	JR	MMULB1
ENDMUL	POP	HL
	EX	DE,HL
	RET 

;	HL:DE /	BC => DE:IX HL-OSTATOK
DIV32	LD	HX,D
	LD	LX,E
	EX	DE,HL
	LD	HL,0
	LD	A,#20
DIV001	ADD	IX,IX
	EX	DE,HL
	ADC	HL,HL
	EX	DE,HL
	ADC	HL,HL

	JR	NC,DIV003
	SBC	HL,BC
DIV002	INC	IX
	DEC	A
	JR	NZ,DIV001
	RET 
DIV003	SBC	HL,BC
	JR	NC,DIV002
	ADD	HL,BC
	DEC	A
	JR	NZ,DIV001
	RET 

;MULTIPLICATION	ROUTINE	:
;
;Multiplies BC by HL-DE
;giving	32 bit result in HL-IX
; HL:DE	* BC =>	HL:IX

MUL32
	LD	IX,0
	ADD	IX,IX
	ADC	HL,HL
	RL	E
	RL	D
	JP	NC,$+9
	ADD	IX,BC
	JP	NC,$+4
	INC	HL
	ADD	IX,IX
	ADC	HL,HL
	RL	E
	RL	D
	JP	NC,$+9
	ADD	IX,BC
	JP	NC,$+4
	INC	HL
	ADD	IX,IX
	ADC	HL,HL
	RL	E
	RL	D
	JP	NC,$+9
	ADD	IX,BC
	JP	NC,$+4
	INC	HL
	ADD	IX,IX
	ADC	HL,HL
	RL	E
	RL	D
	JP	NC,$+9
	ADD	IX,BC
	JP	NC,$+4
	INC	HL
	ADD	IX,IX
	ADC	HL,HL
	RL	E
	RL	D
	JP	NC,$+9
	ADD	IX,BC
	JP	NC,$+4
	INC	HL
	ADD	IX,IX
	ADC	HL,HL
	RL	E
	RL	D
	JP	NC,$+9
	ADD	IX,BC
	JP	NC,$+4
	INC	HL
	ADD	IX,IX
	ADC	HL,HL
	RL	E
	RL	D
	JP	NC,$+9
	ADD	IX,BC
	JP	NC,$+4
	INC	HL
	ADD	IX,IX
	ADC	HL,HL
	RL	E
	RL	D
	JP	NC,$+9
	ADD	IX,BC
	JP	NC,$+4
	INC	HL
	ADD	IX,IX
	ADC	HL,HL
	RL	E
	RL	D
	JP	NC,$+9
	ADD	IX,BC
	JP	NC,$+4
	INC	HL
	ADD	IX,IX
	ADC	HL,HL
	RL	E
	RL	D
	JP	NC,$+9
	ADD	IX,BC
	JP	NC,$+4
	INC	HL
	ADD	IX,IX
	ADC	HL,HL
	RL	E
	RL	D
	JP	NC,$+9
	ADD	IX,BC
	JP	NC,$+4
	INC	HL
	ADD	IX,IX
	ADC	HL,HL
	RL	E
	RL	D
	JP	NC,$+9
	ADD	IX,BC
	JP	NC,$+4
	INC	HL
	ADD	IX,IX
	ADC	HL,HL
	RL	E
	RL	D
	JP	NC,$+9
	ADD	IX,BC
	JP	NC,$+4
	INC	HL
	ADD	IX,IX
	ADC	HL,HL
	RL	E
	RL	D
	JP	NC,$+9
	ADD	IX,BC
	JP	NC,$+4
	INC	HL
	ADD	IX,IX
	ADC	HL,HL
	RL	E
	RL	D
	JP	NC,$+9
	ADD	IX,BC
	JP	NC,$+4
	INC	HL
	ADD	IX,IX
	ADC	HL,HL
	RL	E
	RL	D
	JP	NC,$+9
	ADD	IX,BC
	JP	NC,$+4
	INC	HL
	ADD	IX,IX
	ADC	HL,HL
	RL	E
	RL	D
	JP	NC,$+9
	ADD	IX,BC
	JP	NC,$+4
	INC	HL
	ADD	IX,IX
	ADC	HL,HL
	RL	E
	RL	D
	JP	NC,$+9
	ADD	IX,BC
	JP	NC,$+4
	INC	HL
	ADD	IX,IX
	ADC	HL,HL
	RL	E
	RL	D
	JP	NC,$+9
	ADD	IX,BC
	JP	NC,$+4
	INC	HL
	ADD	IX,IX
	ADC	HL,HL
	RL	E
	RL	D
	JP	NC,$+9
	ADD	IX,BC
	JP	NC,$+4
	INC	HL
	ADD	IX,IX
	ADC	HL,HL
	RL	E
	RL	D
	JP	NC,$+9
	ADD	IX,BC
	JP	NC,$+4
	INC	HL
	ADD	IX,IX
	ADC	HL,HL
	RL	E
	RL	D
	JP	NC,$+9
	ADD	IX,BC
	JP	NC,$+4
	INC	HL
	ADD	IX,IX
	ADC	HL,HL
	RL	E
	RL	D
	JP	NC,$+9
	ADD	IX,BC
	JP	NC,$+4
	INC	HL
	ADD	IX,IX
	ADC	HL,HL
	RL	E
	RL	D
	JP	NC,$+9
	ADD	IX,BC
	JP	NC,$+4
	INC	HL
	ADD	IX,IX
	ADC	HL,HL
	RL	E
	RL	D
	JP	NC,$+9
	ADD	IX,BC
	JP	NC,$+4
	INC	HL
	ADD	IX,IX
	ADC	HL,HL
	RL	E
	RL	D
	JP	NC,$+9
	ADD	IX,BC
	JP	NC,$+4
	INC	HL
	ADD	IX,IX
	ADC	HL,HL
	RL	E
	RL	D
	JP	NC,$+9
	ADD	IX,BC
	JP	NC,$+4
	INC	HL
	ADD	IX,IX
	ADC	HL,HL
	RL	E
	RL	D
	JP	NC,$+9
	ADD	IX,BC
	JP	NC,$+4
	INC	HL
	ADD	IX,IX
	ADC	HL,HL
	RL	E
	RL	D
	JP	NC,$+9
	ADD	IX,BC
	JP	NC,$+4
	INC	HL
	ADD	IX,IX
	ADC	HL,HL
	RL	E
	RL	D
	JP	NC,$+9
	ADD	IX,BC
	JP	NC,$+4
	INC	HL
	ADD	IX,IX
	ADC	HL,HL
	RL	E
	RL	D
	JP	NC,$+9
	ADD	IX,BC
	JP	NC,$+4
	INC	HL
	ADD	IX,IX
	ADC	HL,HL
	RL	E
	RL	D
	JP	NC,$+9
	ADD	IX,BC
	JP	NC,$+4
	INC	HL
	RET 

PHEX	LD	E,A
	RRCA 
	RRCA 
	RRCA 
	RRCA 
	AND	#0F
	ADD	A,#30
	CP	#3A
	JR	C,PHEX2
	ADD	A,7
PHEX2	CALL	PRINT
	LD	A,E
	AND	#0F
	ADD	A,#30
	CP	#3A
	JR	C,PHEX3
	ADD	A,7
PHEX3	CALL	PRINT
	RET 

IX_BUF	DEFB	"0000000000",0

REMORF	LD	HL,IX_BUF+9
	LD	BC,#20FF
	LDD 
	LDD 
	LDD 
	DEC	DE
	LDD 
	LDD 
	LDD 
	DEC	DE
	LDD 
	LDD 
	LDD 
	DEC	DE
	LDD 
	RET 

MAKE_LN	LD	IX,IX_BUF
	EXX 
	LD	DE,#3B9A ; 1000000000
	EXX 
	LD	DE,#CA00
	CALL	GET_DIG
	EXX 
	LD	DE,#05F5 ; 100000000
	EXX 
	LD	DE,#E100
	CALL	GET_DIG
	EXX 
	LD	DE,#0098 ; 10000000
	EXX 
	LD	DE,#9680
	CALL	GET_DIG
	EXX 
	LD	DE,#000F ; 1000000
	EXX 
	LD	DE,#4240
	CALL	GET_DIG
	EXX 
	LD	DE,#0001 ; 100000
	EXX 
	LD	DE,#86A0
	CALL	GET_DIG
	EXX 
	LD	DE,#0000 ; 10000
	EXX 
	LD	DE,#2710
	CALL	GET_DIG
	EXX 
	LD	DE,#0000 ; 1000
	EXX 
	LD	DE,#03E8
	CALL	GET_DIG
	EXX 
	LD	DE,#0000 ; 100
	EXX 
	LD	DE,#0064
	CALL	GET_DIG
	EXX 
	LD	DE,#0000 ; 10
	EXX 
	LD	DE,#000A
	CALL	GET_DIG
	LD	A,L	 ; 1
	ADD	A,#30
	LD	(IX),A
	INC	IX
	LD	HL,IX_BUF
	LD	DE,#2030
KILLZ	LD	A,(HL)
	CP	E
	JR	NZ,KILLZ2
	LD	(HL),D
	INC	HL
	JP	KILLZ
KILLZ2	LD	HL,IX_BUF+9
	LD	A,(HL)
	CP	D
	RET	NZ
	LD	(HL),E
	RET 

; HL:HL	- DE:DE

GET_DIG	XOR	A
INC_DG	INC	A
	SBC	HL,DE
	EXX 
	SBC	HL,DE
	EXX 
	JP	NC,INC_DG
	ADD	HL,DE
	EXX 
	ADC	HL,DE
	EXX 
	DEC	A
	ADD	A,#30
	LD	(IX),A
	INC	IX
	RET 

GSERIAL	LD	HL,(RND+1)
	LD	A,R
	ADD	A,L
	LD	L,A
	LD	A,R
	SUB	H
	LD	H,A
	LD	(RND+1),HL
	LD	A,#FF
	CALL	RND
	LD	(SERIAL+0),A
	LD	A,#FF
	CALL	RND
	LD	(SERIAL+1),A
	LD	A,#FF
	CALL	RND
	LD	(SERIAL+2),A
	LD	A,#FF
	CALL	RND
	LD	(SERIAL+3),A
	RET 

RND	LD	HL,#0000
	EX	AF,AF'
	LD	DE,7
	ADD	HL,DE
	LD	D,H
	LD	E,L
	ADD	HL,HL
	ADD	HL,HL
	LD	B,H
	LD	C,L
	ADD	HL,HL
	ADD	HL,BC
	ADD	HL,DE
	LD	(RND+1),HL
	LD	L,H
	LD	H,0
	EX	AF,AF'
	LD	D,H
	LD	E,A
	CALL	MULT
	LD	A,H
	EX	AF,AF'
	LD	HL,(RND+1)
	LD	A,R
	ADD	A,L
	LD	L,A
	LD	A,R
	SUB	H
	LD	H,A
	LD	(RND+1),HL
	EX	AF,AF'
	RET 

MULT	LD	B,#10
	LD	A,H
	LD	C,L
	LD	HL,0
MULT1	ADD	HL,HL
	RET	C
	RL	C
	RLA 
	JR	NC,MULT3
	ADD	HL,DE
	RET	C
MULT3	DJNZ	MULT1
	RET 


CLR_FAT	IN	A,(PAGE3)
	PUSH	AF
	LD	BC,0
CLRF1	LD	HL,FATPAGE
	ADD	HL,BC
	LD	A,(HL)
	OUT	(PAGE3),A
	EXX 
	LD	HL,#C000
	LD	DE,#C001
	LD	BC,#3FFF
	LD	(HL),L
	LDIR 
	EXX 
	INC	C
	LD	A,8
	CP	C
	JR	NZ,CLRF1
	POP	AF
	OUT	(PAGE3),A
	LD	HL,#0000
	LD	DE,#FFF8
	CALL	MARKFAT
	LD	HL,#0001
	LD	DE,#FFFF
	CALL	MARKFAT
	RET 

; HL - CLUSTER
; DE - (CLUSTER)

MARKFAT	IN	A,(PAGE3)
	PUSH	AF
	PUSH	HL
	LD	A,H
	RLCA 
	RLCA 
	RLCA 
	AND	#07
	LD	L,A
	LD	H,0
	LD	BC,FATPAGE
	ADD	HL,BC
	LD	A,(HL)
	OUT	(PAGE3),A
	POP	HL
	LD	A,H
	AND	#1F
	LD	H,A
	LD	BC,#C000
	ADD	HL,HL
	ADD	HL,BC
	LD	(HL),E
	INC	HL
	LD	(HL),D
	POP	AF
	OUT	(PAGE3),A
	RET 

MEM_ID	DEFB	#00

FATPAGE	DEFB	#00,#00,#00,#00,#00,#00,#00,#00


;SAVEFAT	RET 
SAVEFAT	IN	A,(PAGE3)
	PUSH	AF
	LD	IX,#0000
	LD	DE,#0000
	LD	HL,BPB_
	LD	BC,#0106
	LD	A,(FDRIVE)
	SUB	"C"
	ADD	A,2
	RST	#18

	LD	BC,0
	LD	(PLUS),BC
	LD	HL,(S_P_FAT)
SAVFAT1	PUSH	HL
	LD	HL,FATPAGE
	ADD	HL,BC
	LD	A,(HL)
	OUT	(PAGE3),A
	POP	HL
	PUSH	BC
	LD	BC,#0020
	AND	A
	SBC	HL,BC
	PUSH	HL
	JR	NC,NORSAVE
	ADD	HL,BC
	LD	B,L
	LD	C,#06
	PUSH	BC
	LD	IX,#0000
	LD	HL,(FAT1_XX)
	LD	DE,(PLUS)
	ADD	HL,DE
	EX	DE,HL
	LD	HL,#C000
	LD	A,(FDRIVE)
	SUB	"C"
	ADD	A,2
	RST	#18
	POP	BC
	LD	IX,#0000
	LD	HL,(FAT2_XX)
	LD	DE,(PLUS)
	ADD	HL,DE
	EX	DE,HL
	LD	HL,#C000
	LD	A,(FDRIVE)
	SUB	"C"
	ADD	A,2
	RST	#18

;clearing root directory
	LD	A,#FF
	OUT	(PAGE3),A

ROOTBUF	EQU	#C000

	LD	HL,ROOTBUF
	LD	DE,ROOTBUF+1
	LD	BC,#3FFF
	LD	(HL),0
	LDIR

	POP	HL
	POP	BC
	LD	IX,#0000
	LD	DE,(DIR_FRM)
	LD	HL,#C000
	LD	BC,#2006
	LD	A,(FDRIVE)
	SUB	"C"
	ADD	A,2
	RST	#18	

	POP	AF
	OUT	(PAGE3),A
	RET 

NORSAVE	LD	IX,#0000
	LD	HL,(FAT1_XX)
	LD	DE,(PLUS)
	ADD	HL,DE
	EX	DE,HL
	LD	BC,#2006
	LD	HL,#C000
	LD	A,(FDRIVE)
	SUB	"C"
	ADD	A,2
	RST	#18
	LD	IX,#0000
	LD	HL,(FAT2_XX)
	LD	DE,(PLUS)
	ADD	HL,DE
	EX	DE,HL
	LD	BC,#2006
	LD	HL,#C000
	LD	A,(FDRIVE)
	SUB	"C"
	ADD	A,2
	RST	#18
	LD	HL,(PLUS)
	LD	BC,#0020
	ADD	HL,BC
	LD	(PLUS),HL
	POP	HL
	POP	BC
	INC	C
	JP	SAVFAT1

BPB_	DEFB	#EB,#3C,#90
	DEFB	"MSDOS5.0"
B_P_S	DEFW	#0200
S_P_C	DEFB	#04	;+
RESERVE	DEFW	#0001
FAT_P_D	DEFB	#02
FILES	DEFW	#0200
	DEFW	#0000
	DEFB	#F8
S_P_FAT	DEFW	#0051	;+
S_P_T	DEFW	#003F
H_P_C	DEFW	#0040
HIDD_L	DEFW	#003F
HIDD_H	DEFW	#0000
ALLSECL	DEFW	#0000
ALLSECH	DEFW	#0000
	DEFW	#0080
	DEFB	#29
SERIAL	DEFW	#0000
	DEFW	#0000
LABEL	DEFB	"NO NAME    "
	DEFB	"FAT16	 "
	DEFB	#FA,#33
	DEFB	#C0,#8E,#D0,#BC,#00,#7C,#16,#07,#BB,#78,#00,#36,#C5,#37,#1E,#56
	DEFB	#16,#53,#BF,#3E,#7C,#B9,#0B,#00,#FC,#F3,#A4,#06,#1F,#C6,#45,#FE
	DEFB	#0F,#8B,#0E,#18,#7C,#88,#4D,#F9,#89,#47,#02,#C7,#07,#3E,#7C,#FB
	DEFB	#CD,#13,#72,#79,#33,#C0,#39,#06,#13,#7C,#74,#08,#8B,#0E,#13,#7C
	DEFB	#89,#0E,#20,#7C,#A0,#10,#7C,#F7,#26,#16,#7C,#03,#06,#1C,#7C,#13
	DEFB	#16,#1E,#7C,#03,#06,#0E,#7C,#83,#D2,#00,#A3,#50,#7C,#89,#16,#52
	DEFB	#7C,#A3,#49,#7C,#89,#16,#4B,#7C,#B8,#20,#00,#F7,#26,#11,#7C,#8B
	DEFB	#1E,#0B,#7C,#03,#C3,#48,#F7,#F3,#01,#06,#49,#7C,#83,#16,#4B,#7C
	DEFB	#00,#BB,#00,#05,#8B,#16,#52,#7C,#A1,#50,#7C,#E8,#92,#00,#72,#1D
	DEFB	#B0,#01,#E8,#AC,#00,#72,#16,#8B,#FB,#B9,#0B,#00,#BE,#E6,#7D,#F3
	DEFB	#A6,#75,#0A,#8D,#7F,#20,#B9,#0B,#00,#F3,#A6,#74,#18,#BE,#9E,#7D
	DEFB	#E8,#5F,#00,#33,#C0,#CD,#16,#5E,#1F,#8F,#04,#8F,#44,#02,#CD,#19
	DEFB	#58,#58,#58,#EB,#E8,#8B,#47,#1A,#48,#48,#8A,#1E,#0D,#7C,#32,#FF
	DEFB	#F7,#E3,#03,#06,#49,#7C,#13,#16,#4B,#7C,#BB,#00,#07,#B9,#03,#00
	DEFB	#50,#52,#51,#E8,#3A,#00,#72,#D8,#B0,#01,#E8,#54,#00,#59,#5A,#58
	DEFB	#72,#BB,#05,#01,#00,#83,#D2,#00,#03,#1E,#0B,#7C,#E2,#E2,#8A,#2E
	DEFB	#15,#7C,#8A,#16,#24,#7C,#8B,#1E,#49,#7C,#A1,#4B,#7C,#EA,#00,#00
	DEFB	#70,#00,#AC,#0A,#C0,#74,#29,#B4,#0E,#BB,#07,#00,#CD,#10,#EB,#F2
	DEFB	#3B,#16,#18,#7C,#73,#19,#F7,#36,#18,#7C,#FE,#C2,#88,#16,#4F,#7C
	DEFB	#33,#D2,#F7,#36,#1A,#7C,#88,#16,#25,#7C,#A3,#4D,#7C,#F8,#C3,#F9
	DEFB	#C3,#B4,#02,#8B,#16,#4D,#7C,#B1,#06,#D2,#E6,#0A,#36,#4F,#7C,#8B
	DEFB	#CA,#86,#E9,#8A,#16,#24,#7C,#8A,#36,#25,#7C,#CD,#13,#C3,#0D,#0A
	DEFB	#4E,#6F,#6E,#2D,#53,#79,#73,#74,#65,#6D,#20,#64,#69,#73,#6B,#20
	DEFB	#6F,#72,#20,#64,#69,#73,#6B,#20,#65,#72,#72,#6F,#72,#0D,#0A,#52
	DEFB	#65,#70,#6C,#61,#63,#65,#20,#61,#6E,#64,#20,#70,#72,#65,#73,#73
	DEFB	#20,#61,#6E,#79,#20,#6B,#65,#79,#20,#77,#68,#65,#6E,#20,#72,#65
	DEFB	#61,#64,#79,#0D,#0A,#00,#49,#4F,#20,#20,#20,#20,#20,#20,#53,#59
	DEFB	#53,#4D,#53,#44,#4F,#53,#20,#20,#20,#53,#59,#53,#00,#00,#55,#AA

	ORG	#C000
	INCBIN  DOS_OBJ
	ORG	ORG_
              