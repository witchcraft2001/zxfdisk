
ORG_	ORG	#8000
IPOINT	EQU	#3D13
	DI 
	XOR	A
	LD	C,0
	CALL	HDDRIVE
	LD	(DEVC),A
	OR	A
	EI 
	RET	Z
	DI 
	LD	BC,#0380
	LD	E,1
	CALL	IPOINT
	XOR	A
	LD	(DRV),A
	CALL	PARTDET
MAIN	CALL	KEY
	LD	HL,KEYTAB
NKEY	CP	(HL)
	JR	Z,YKEY
	INC	HL
	INC	HL
	INC	HL
	INC	(HL)
	DEC	(HL)
	JR	NZ,NKEY
	JP	MAIN 

EXIT	POP	DE
	LD	BC,#0080
	LD	E,0
	CALL	IPOINT
	LD	IY,#5C3A
	EI 
	RET 

YKEY	LD	DE,MAIN	
	PUSH	DE
	INC	HL
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	PUSH	DE
	RET 

KEY	RES	5,(IY+1)
	EI 
KEY2	BIT	5,(IY+1)
	JR	Z,KEY2
	LD	A,(23560)
	RET 

KEYTAB	DEFB	" "
	DEFW	EXIT
	DEFB	"1"
	DEFW	MASTERD
	DEFB	"2"
	DEFW	SLAVED
	DEFB	0

MASTERD	LD	A,0
	LD	(DRV),A
	CALL	PARTDET
	RET 

SLAVED	LD	A,(DEVC)
	DEC	A
	RET	Z
	LD	A,1
	LD	(DRV),A
	CALL	PARTDET
	RET 

PARTDET	LD	HL,SECTORS
	LD	(OFFSECT),HL
	LD	HL,0
	LD	(EXTDOSL),HL
	LD	(EXTDOSH),HL
	LD	DE,0
	LD	BC,#4F89
	LD	HL,#2050
	CALL	IPOINT
	LD	DE,#0000
	LD	HL,HEAD0
	CALL	STRING
	LD	DE,#0200
	LD	HL,HEAD1
	CALL	STRING
	CALL	CRLF
	CALL	PARTIT
	CALL	DISPLAY
	RET 

LOADSEC	LD	IX,(SECTORH)
	LD	DE,(SECTORL)
	LD	HL,SECTOR
	LD	BC,#0105
	LD	A,(DRV)
	CALL	HDDRIVE
	RET 

CENTER	LD	D,H
	LD	E,L
	LD	BC,80
	XOR	A
	CPIR 
	LD	H,D
	LD	L,E
	LD	A,80
	SUB	C
	SRL	A
	LD	C,A
	LD	A,40
	SUB	C
	LD	DE,(CUR_POS)
	LD	E,A
	CALL	LOCAT
	JP	PRINTZ

CUR_POS	DEFW	#0000

PRINT	LD	BC,#0182
	JP	IPOINT

PRINTB	LD	C,#82
	JP	IPOINT

STRING	CALL	LOCAT
PRINTZ	LD	BC,#508C
	LD	D,0
	JP	IPOINT

PRINTC	LD	C,#86
	JP	IPOINT

CRLF	LD	DE,(CUR_POS)
	LD	E,0
	INC	D
LOCAT	LD	C,#84
	LD	(CUR_POS),DE
	JP	IPOINT

HLOCAT	LD	C,#84
	JP	IPOINT

PDIGIT	LD	DE,10000
	CALL	DIG
	LD	DE,1000
	CALL	DIG
	LD	DE,100
	CALL	DIG
	LD	DE,10
	CALL	DIG
	LD	A,L
	ADD	A,#30
	JP	PRINT

DIG	XOR	A
DIG1	INC	A
	SBC	HL,DE
	JR	NC,DIG1
	ADD	HL,DE
	DEC	A
;	RET	Z
	ADD	A,#30
	PUSH	HL
	CALL	PRINT
	POP	HL
	RET 

PHEX	LD	E,A
	RRCA 
	RRCA 
	RRCA 
	RRCA 
	AND	#0F
	ADD	A,#30
	CP	#3A
	JR	C,PHEX2
	ADD	A,7
PHEX2	CALL	PRINT
	LD	A,E
	AND	#0F
	ADD	A,#30
	CP	#3A
	JR	C,PHEX3
	ADD	A,7
PHEX3	CALL	PRINT
	RET 

PHEXSTR	LD	B,#10
PHS1	PUSH	BC
	LD	A,(HL)
	CALL	PHEX
	LD	A,#20
	CALL	PRINT
	POP	BC
	INC	HL
	DJNZ	PHS1
	RET 

PHDIG	XOR	A
	ADD	HL,HL
	ADC	A,A
	ADD	HL,HL
	ADC	A,A
	ADD	HL,HL
	ADC	A,A
	ADD	HL,HL
	ADC	A,A
;	OR	A
;	RET	Z
	ADD	A,#30
	CP	#3A
	JR	C,PHDI2
	ADD	A,7
PHDI2	PUSH	HL
	CALL	PRINT
	POP	HL
	RET 

; HL':HL

BIGDIG	EXX 
	LD	DE,#3B9A ; 1000000000
	EXX 
	LD	DE,#CA00
	CALL	GET_DIG
	EXX 
	LD	DE,#05F5 ; 100000000
	EXX 
	LD	DE,#E100
	CALL	GET_DIG
	EXX 
	LD	DE,#0098 ; 10000000
	EXX 
	LD	DE,#9680
	CALL	GET_DIG
	EXX 
	LD	DE,#000F ; 1000000
	EXX 
	LD	DE,#4240
	CALL	GET_DIG
	EXX 
	LD	DE,#0001 ; 100000
	EXX 
	LD	DE,#86A0
	CALL	GET_DIG
	EXX 
	LD	DE,#0000 ; 10000
	EXX 
	LD	DE,#2710
	CALL	GET_DIG
	EXX 
	LD	DE,#0000 ; 1000
	EXX 
	LD	DE,#03E8
	CALL	GET_DIG
	EXX 
	LD	DE,#0000 ; 100
	EXX 
	LD	DE,#0064
	CALL	GET_DIG
	EXX 
	LD	DE,#0000 ; 10
	EXX 
	LD	DE,#000A
	CALL	GET_DIG
	LD	A,L	 ; 1
	ADD	A,#30
	CALL	PRINT
	LD	A," "
	CALL	PRINT
	RET 

; HL:HL	- DE:DE

GET_DIG	XOR	A
INC_DG	INC	A
	SBC	HL,DE
	EXX 
	SBC	HL,DE
	EXX 
	JP	NC,INC_DG
	ADD	HL,DE
	EXX 
	ADC	HL,DE
	EXX 
	DEC	A
;	RET	Z
	ADD	A,#30
	PUSH	HL
	PUSH	DE
	EXX 
	PUSH	HL
	PUSH	DE
	CALL	PRINT
	POP	DE
	POP	HL
	EXX 
	POP	DE
	POP	HL
	RET 

DINPUT	CALL	INPUT
	LD	DE,INBUF
	CALL	STR2NUM
	RET 

INPUT	LD	IX,INBUF
	LD	HL,INBUF
	LD	(HL),"0"
	INC	HL
	LD	D,H
	LD	E,L
	INC	DE
	LD	(HL)," "
	LD	BC,8
	LDIR 
INPUT1	LD	DE,#0140
	CALL	LOCAT
	LD	HL,INBUF
	CALL	PRINTZ
;	CALL	KEY
	CP	#0D
	RET	Z
	CP	#0C
	JR	Z,BACK
	CP	"a"
	JR	C,INPUT2
	RES	5,A
INPUT2	LD	(IX),A
	INC	IX
	JR	INPUT1
BACK	LD	D,HX
	LD	E,LX
	LD	HL,INBUF
	AND	A
	SBC	HL,DE
	JR	Z,INPUT1
	DEC	IX
	LD	A,#20
	LD	(IX),A
	JR	INPUT1

; DE - "12345"
; HL:IX	- NUMBER

STR2NUM	LD	HL,0
	LD	IX,0
STR2N01	LD	A,(DE)
	INC	DE
	OR	A
	RET	Z
	CP	#20
	RET	Z
	ADD	IX,IX
	ADC	HL,HL
	ADD	IX,IX
	ADC	HL,HL
	ADD	IX,IX
	ADC	HL,HL
	ADD	IX,IX
	ADC	HL,HL
	CALL	CHEXDIG
	RET	C
	LD	B,0
	LD	C,A
	ADD	IX,BC
	LD	C,B
	ADC	HL,BC
	JR	STR2N01

CHEXDIG	CP	"0"
	JR	C,STRB1
	CP	":"
	JR	NC,STRB2
	SUB	"0"
	AND	A
	RET 
STRB2	CP	"A"
	JR	C,STRB1
	CP	"G"
	JR	NC,STRB1
	SUB	"A"-10
	AND	A
	RET 
STRB1	SCF 
	RET 

INCLUDE	"IDE_DRV

INBUF	DEFB	"0	   "
	DEFS	#100,0

HEAD0	DEFB	"Display Partition Information",0
HEAD1	DEFB	"Logical drive	 Size	    Offset ",0

;		 C:12345678901	 2048	  4000000000

DISPLAY	XOR	A
	LD	HL,(OFFSECT)
	LD	DE,SECTORS
	SBC	HL,DE
	SRL	H
	RR	L
	SRL	H
	RR	L
	SRL	H
	RR	L
	LD	B,L
	LD	C,A
	INC	B
	DEC	B
	RET	Z
	LD	(OFFSECT),DE
DISPLA1	LD	DE,(CUR_POS)
	LD	E,0
	PUSH	BC
	LD	L,C
	PUSH	DE
	CALL	LOCAT
	LD	A,"C"
	ADD	A,L
	CALL	PRINT
	LD	A,":"
	CALL	PRINT
	POP	DE
	LD	E,#10
	PUSH	DE
	CALL	LOCAT
	LD	IX,(OFFSECT)
	LD	L,(IX+5)
	LD	H,(IX+6)
	LD	D,(IX+7)
	XOR	A
	SRL	D
	RR	H
	RR	L
	RR	A
	SRL	D
	RR	H
	RR	L
	RR	A
	SRL	D
	RR	H
	RR	L
	RR	A
	OR	A
	JR	Z,NOINC
	INC	HL
NOINC	CALL	PDIGIT
	POP	DE
	LD	E,#19
	CALL	LOCAT
	LD	IX,(OFFSECT)
	LD	L,(IX+0)
	LD	H,(IX+1)
	EXX 
	LD	L,(IX+2)
	LD	H,(IX+3)
	EXX 
	CALL	BIGDIG
	CALL	CRLF
	LD	IX,(OFFSECT)
	LD	DE,8
	ADD	IX,DE
	LD	(OFFSECT),IX
	POP	BC
	INC	C
	DJNZ	DISPLA1
	RET 


EASYDOS
MEDIDOS
HIGHDOS	LD	E,(IY+08)
	LD	D,(IY+09)
	LD	L,(IY+10)
	LD	H,(IY+11)
	LD	IX,(SECTORL)
	ADD	IX,DE
	LD	DE,(SECTORH)
	ADC	HL,DE
	LD	D,HX
	LD	E,LX
	LD	IX,(OFFSECT)
	LD	(IX+0),E
	LD	(IX+1),D
	LD	(IX+2),L
	LD	(IX+3),H
	LD	E,(IY+12)
	LD	D,(IY+13)
	LD	L,(IY+14)
	LD	H,(IY+15)
	LD	(IX+4),E
	LD	(IX+5),D
	LD	(IX+6),L
	LD	(IX+7),H
	LD	DE,8
	ADD	IX,DE
	LD	(OFFSECT),IX
NXTPART	LD	DE,#10
	ADD	IY,DE
	POP	BC
	DJNZ	DOSAGA
	AND	A
	RET 

PARTIT	LD	IX,0
	LD	DE,0
PARTIT2	LD	(SECTORL),DE
	LD	(SECTORH),IX
	CALL	LOADSEC
	LD	HL,(SECTOR+510)
	LD	DE,#AA55
	AND	A
	SBC	HL,DE
	JR	NZ,NODEFIN
	LD	IY,SECTOR+#01BE
	LD	B,4
DOSAGA	PUSH	BC
	LD	A,(IY+4)
	CP	5
	JR	NZ,NOEXTDS
	PUSH	IY
	LD	DE,(SECTORL)
	LD	IX,(SECTORH)
	PUSH	DE
	PUSH	IX
	CALL	EXTDOS
	POP	IX
	POP	DE
	LD	(SECTORL),DE
	LD	(SECTORH),IX
	CALL	LOADSEC
	POP	IY
	JP	NXTPART
NOEXTDS	CP	6
	JP	Z,HIGHDOS
	CP	4
	JP	Z,MEDIDOS
	CP	1
	JP	Z,EASYDOS
	POP	BC
	OR	A
	RET	Z
NODEFIN	SCF 
	RET 

EXTDOS
	LD	HL,(EXTDOSL)
	LD	DE,(EXTDOSH)
	LD	A,L
	OR	H
	OR	E
	OR	D
	LD	E,(IY+08)
	LD	D,(IY+09)
	LD	L,(IY+10)
	LD	H,(IY+11)
	JP	NZ,EXTDOS2
	LD	(EXTDOSL),DE
	LD	(EXTDOSH),HL
	LD	IX,(EXTDOSH)
	JP	PARTIT2

EXTDOS2	LD	IX,(EXTDOSL)
	ADD	IX,DE
	PUSH	IX
	LD	DE,(EXTDOSH)
	ADC	HL,DE
	PUSH	HL
	POP	IX
	POP	DE
	JP	PARTIT2

DEVC	DEFB	#00

DRV	DEFB	#00

SECTORL	DEFW	#0000
SECTORH	DEFW	#0000

EXTDOSL	DEFW	#0000
EXTDOSH	DEFW	#0000

OFFSECT	DEFW	SECTORS

SECTORS	DEFS	26*4,#FF

SECTOR	DEFS	#200,0



